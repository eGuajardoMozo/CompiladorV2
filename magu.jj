/*
	MAGU Scanner/Parser
	Adrián Martínez A01280252
	Estefanía Guajardo A00813202
*/

// Para permitir el uso de variables y que los métodos no sean estáticos por default
options {
	STATIC = false ;
}

PARSER_BEGIN(magu)

import java.util.*;

class magu
{

	public static void main(String[] args) throws ParseException
	{
		try
		{
			magu scanner = new magu(System.in);
			scanner.Programa();
			System.out.println("OK!");
		}
		catch(ParseException e)
		{
			System.out.println(e.getMessage());
			System.out.println("Error!");
		}
	}

	Stack pOperators = new Stack();
	Stack pOperands = new Stack();
	Stack pJumps = new Stack();

	Vector cuadruplos = new Vector(1);

	int currentTemporal  = 1;
	int currentString = 1;
	int quadCounter = 0;	
}
PARSER_END(magu)

TOKEN:
{
		< TK_FORWARD : "forward" | "f" >		{}
	|	< TK_LEFT : "left" | "l" >				{}
	|	< TK_RIGHT : "right" | "r">				{}
	|	< TK_PENCILUP : "pencilup" | "pu" >		{}
	|	< TK_PENCILDOWN : "pencildown" | "pd" >	{}
	|	< TK_MOVE : "move" | "m" >				{}
	|	< TK_HOME : "home" | "h" >				{}
	|	< TK_CURVE : "curve" | "cv" >			{}
	|	< TK_INPUT : "input" | "in" >			{}
	|	< TK_PRINT : "print" | "pr" >			{}
	|	< TK_WHILE : "while" >					{}
	|	< TK_IF : "if" >						{}
	|	< TK_ELSE : "else" >					{}
	|	< TK_ARR : "arr" >						{}
	|	< TK_FUNC : "func" >					{}
	|	< TK_MAIN : "main" >					{}
	|	< TK_NE: "<>" >							{}
	|	< TK_EQT: "==" >						{}
	|	< TK_LT: "<" >							{}
	|	< TK_GT: ">" >							{}
	|	< TK_EQ: "=" >							{}
	|	< TK_LPAR: "(" >						{}
	|	< TK_RPAR: ")" >						{}
	|	< TK_LBRACKET: "[" >					{}
	|	< TK_RBRACKET: "]" >					{}
	|	< TK_LBRACE: "{" >						{}
	|	< TK_RBRACE: "}" >						{}
	|	< TK_COMMA: "," >						{}
	|	< TK_PLUS: "+" >						{}
	|	< TK_MINUS: "-" >						{}
	|	< TK_MULT: "*" >						{}
	|	< TK_DIV: "/" >							{}	
	|	< TK_CTE_I : (["0"-"9"])+ >				{}
	|	< TK_ID :  ["a"-"z","A"-"Z"](["a"-"z","A"-"Z","0"-"9"])* > {}
	|   < TK_STRING : <QUOTE> (~["\r","\n","\"" ])+ <QUOTE> > {}
	|	< #QUOTE : "\"" >
}

SKIP:
{
	" "	| "\r\n" | "\t"
}

void Programa() : { Memoria.addFunc("global"); } // Agrega la tabla de variables locales a memoria
{   
    ( Asignacion() )*
    ( Arreglo() )*

    {
		Cuadruplo mainQuad = new Cuadruplo("main", "", "", "");
		quadCounter++;
		cuadruplos.addElement(mainQuad); // Agregarlo a la queue de cuadruplos

		pJumps.push("" + (quadCounter-1));	
	}

    ( Definicion_Func() )*
    
    <TK_MAIN> <TK_LPAR> <TK_RPAR>

	{
		String main = (String) pJumps.pop();
		Fill ( Integer.parseInt(main) , quadCounter);

		Memoria.addFunc("main"); // Agrega la función main a memoria
	}

    Secuencia()  <EOF>

    {
    	Cuadruplo quad = new Cuadruplo("end","","",""); // Fin del codigo
    	cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
    	MostrarCuadruplos();

    	MaquinaVirtual mv = new MaquinaVirtual();
		mv.Ejecucion(cuadruplos);
    }
}

void Func() : {String op1, op2, s, s2, index; Token id; Vector userString = new Vector(1);}
{
    ( 
    	  <TK_FORWARD> <TK_LPAR> Exp() <TK_RPAR> 
    	  	{
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("f",op1,"","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_LEFT> <TK_LPAR> Exp() <TK_RPAR>
    	  	{
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("l",op1,"","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_RIGHT> <TK_LPAR>  Exp() <TK_RPAR>
    	  	{
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("r",op1,"","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_INPUT> <TK_LPAR> id = <TK_ID> <TK_RPAR>
    	  	{
    	  		op1 = id.image;

    	  		Cuadruplo quad = new Cuadruplo("in",op1,"","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
	    	
    	| <TK_PRINT> <TK_LPAR> 
    			( 
    				LOOKAHEAD(2)
    				id = <TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET>
    				{
    					index = (String)pOperands.pop();
    					s =  id.image + "-" + index;
    					userString.addElement(s);
    				}
    			|
    				id = <TK_ID>
    				{
    					s = id.image;
    					userString.addElement(s);
    				}			
    			|
    				id = <TK_STRING>
    				{
    					s = id.image;
    					userString.addElement(s);
    				} 
    			)

    			( <TK_PLUS> 
    				( 
    					LOOKAHEAD(2)
    			  		id = <TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET> 
    			  		{ 
    			  			index = (String)pOperands.pop();
    			  			s = id.image + "-" + index;
    			  			userString.addElement(s);
    			  		}
    			  	|
    					id = <TK_ID>
    					{
    						s = id.image;
    						userString.addElement(s); 
    					}
    				|
    					id = <TK_STRING>
    					{
    						s = id.image;
    						userString.addElement(s);
    					}  
    				) 
    			)*
    			<TK_RPAR>

    			{
    				String str = "s" + currentString;

    				Cuadruplo quad = new Cuadruplo("pr", str ,"","");
    				currentString++;
    	  			quadCounter++;
    	  			cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos

    	  			TablaStrings.addString( str, userString ); // Agregar vector de strings a tabla
    			}

    	| <TK_PENCILUP> <TK_LPAR> <TK_RPAR>
    	  	{
    	  		Cuadruplo quad = new Cuadruplo("pu","","","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_PENCILDOWN> <TK_LPAR> <TK_RPAR>
    	  	{
    	  		Cuadruplo quad = new Cuadruplo("pd","","","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_MOVE> <TK_LPAR> Exp() <TK_COMMA> Exp() <TK_RPAR>
    	  	{
    	  		op2 = (String)pOperands.pop();
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("m",op1,op2,"");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_CURVE> <TK_LPAR> Exp() <TK_COMMA> Exp() <TK_RPAR>
    	  	{
    	  		op2 = (String)pOperands.pop();
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("cv",op1,op2,"");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_HOME> <TK_LPAR> <TK_RPAR>
    	  	{
    	  		Cuadruplo quad = new Cuadruplo("h","","","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    )   
}  




void Funcion() : {Token id; int paramCounter = 0, start; String argument;} 
{	
    id = <TK_ID>

    {
    	// Si no existe una función con este id, marcar error y salir
    	if ( !Memoria.contextos.containsKey(id.image) ) {
    		System.out.println("ERROR: La funcion " + id.image + " no existe");
			System.exit(0);
    	}
    } 


    <TK_LPAR> 
    {
    	// Crear cuadruplo que marca la llamada a una función
    	Cuadruplo func = new Cuadruplo("func",id.image,"","");
    	quadCounter++;
    	cuadruplos.addElement(func); // Agregarlo a la queue de cuadruplos
    }

    // ARGUMENTS
    ( 
    	Exp()
    	{
    		paramCounter++;
    		argument = (String)pOperands.pop();

    		Cuadruplo quad = new Cuadruplo("param",argument,"","param" + paramCounter);
    		quadCounter++;
    		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
    	}

    	( <TK_COMMA> {paramCounter++;}
    		Exp() 
    		{
	    		argument = (String)pOperands.pop();

	    		Cuadruplo quad2 = new Cuadruplo("param",argument,"","param" + paramCounter);
	    		quadCounter++;
	    		cuadruplos.addElement(quad2); // Agregarlo a la queue de cuadruplos
    		}
    	)* 
    )? 

    <TK_RPAR>
    {
    	// Generar gosub con el nombre de la función y el cuadruplo donde empieza
    	start = Memoria.getFuncStart(id.image);

    	Cuadruplo gosub = new Cuadruplo("gosub",id.image,"", "" + start );
	    quadCounter++;
	    cuadruplos.addElement(gosub); // Agregarlo a la queue de cuadruplos
    }
}


void Exp() : {}
{
	Termino()

	( 
		<TK_PLUS> 
		{
			pOperators.push("+"); // meter el + a la stack de operadores
		}

		Termino()
		PendingOperator("+","-")

		| 

		<TK_MINUS> 
		{
			pOperators.push("-"); // meter el - a la stack de operadores
		}

		Termino()
		PendingOperator("+","-")

	)*
		
}

void Termino() : {}
{
	Factor()
	
	( 
		<TK_MULT> 
		{
			pOperators.push("*"); // meter el * a la stack de operadores
		}

		Factor()
		PendingOperator("*","/")

		| 

		<TK_DIV>
		{
			pOperators.push("/"); // meter el / a la stack de operadores
		}

		Factor()
		PendingOperator("*","/")

	)*

}

void Factor() : {Token o; String index;}
{
	LOOKAHEAD(2)

	//indice de arreglo
	o = <TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET>
	{
		index = (String)pOperands.pop();
		pOperands.push(o.image + "-" + index );
	}

	|

	// variable
	o = <TK_ID>
	{
		pOperands.push(o.image); // meter el id a la stack de operandos
	}

	|

	//constante
	o = <TK_CTE_I>
	{
		pOperands.push(o.image); // meter la constante a la stack de operandos
	}
	
}


void Asignacion() : {String op1, op2; Token r;}
{
	LOOKAHEAD(2) 
	// variable
	r = <TK_ID> <TK_EQ> Exp()
	{
		op1 = (String)pOperands.pop();

		Cuadruplo quad = new Cuadruplo("=", op1, "", r.image);
		quadCounter++;

		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos

	}
	|
	// indice de arreglo
	r = <TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET> <TK_EQ> Exp()
	{
		op1 = (String)pOperands.pop(); // Valor que se va a asignar
		op2 = (String)pOperands.pop(); // Indice del arreglo

		Cuadruplo quad = new Cuadruplo("=", op1, op2, r.image);
		quadCounter++;

		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos

	}
}


void Bool() : {Token o;}
{
	Exp()
	( 
		o = <TK_LT> Exp()
		{
			pOperators.push(o.image); // meter el < a la stack de operandos
		}
		PendingOperatorBool()

		|
		
		o = <TK_GT>	Exp()
		{
			pOperators.push(o.image); // meter el > a la stack de operandos
		}
		PendingOperatorBool()


		|
		
		o = <TK_NE> Exp()
		{
			pOperators.push(o.image); // meter el <> a la stack de operandos
		}
		PendingOperatorBool()


		|
		
		o = <TK_EQT> Exp()
		{
			pOperators.push(o.image); // meter el == a la stack de operandos
		}
		PendingOperatorBool()

	)?
}

void Secuencia() : {}
{
	<TK_LBRACE> ( LOOKAHEAD(2) Funcion() | LOOKAHEAD(2) Func() | While() | Asignacion() | Condicion() )
	( <TK_COMMA> ( LOOKAHEAD(2) Funcion() | LOOKAHEAD(2) Func() | While() | Asignacion() | Condicion() ) )*
	<TK_RBRACE>
}

void While() : {String op1, r;}
{
	<TK_WHILE>
	{
		pJumps.push("" + (quadCounter+1));
	}


	<TK_LPAR> Bool() <TK_RPAR>
	{
		op1 = (String)pOperands.pop();

		Cuadruplo quadTrue = new Cuadruplo("GotoF", op1, "", "");
		quadCounter++;
		cuadruplos.addElement(quadTrue); // Agregarlo a la queue de cuadruplos
		pJumps.push("" + (quadCounter-1));
	}

	Secuencia()
	{

		String end = (String)pJumps.pop();
		String retorno = (String)pJumps.pop();

		Cuadruplo quadFalse = new Cuadruplo("Goto", "", "", retorno);
		quadCounter++;
		cuadruplos.addElement(quadFalse); // Agregarlo a la queue de cuadruplos

		Fill ( Integer.parseInt(end), quadCounter);
	}
}







void Definicion_Func() : {Token id, var; int numParam = 0;}
{
	<TK_FUNC> id = <TK_ID>
	{
		Memoria.addFunc(id.image); // Se agrega la función a memoria
	}

	<TK_LPAR>
	( 
		// Crea variable param1, param2... cuyo valor es el nombre dado por el usuario
		var = <TK_ID>
		{
			numParam++;
			Memoria.asignarParametro(id.image, "param" + numParam, var.image);
		}

		( 
			<TK_COMMA> 
			var = <TK_ID>
			{
				numParam++;
				Memoria.asignarParametro(id.image, "param" + numParam, var.image);
			}			
		)* 

	)?
	<TK_RPAR>

	// Se agrega el punto de inicio de la función 
	{
		Memoria.setFuncStart(id.image, quadCounter+1 );
	}

	Secuencia()
	{
		Cuadruplo quad = new Cuadruplo("endproc", "", "", "");
		quadCounter++;
		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	}

}



void Condicion() : {String op1, r;}
{
	<TK_IF> <TK_LPAR> Bool() <TK_RPAR>

	{
		op1 = (String)pOperands.pop();

		Cuadruplo quadTrue = new Cuadruplo("GotoF", op1, "", "");
		quadCounter++;

		cuadruplos.addElement(quadTrue); // Agregarlo a la queue de cuadruplos

		pJumps.push("" + (quadCounter-1));
	}
	Secuencia()

	(<TK_ELSE> 
	{
		Cuadruplo quadFalse = new Cuadruplo("Goto", "", "", "");
		quadCounter++;

		cuadruplos.addElement(quadFalse); // Agregarlo a la queue de cuadruplos

		String falso = (String)pJumps.pop();
		pJumps.push("" + (quadCounter-1));
		Fill ( Integer.parseInt(falso), quadCounter);

	}
		Secuencia() )?

	// Fin del if-else
	{
		String end = (String)pJumps.pop();

		Fill ( Integer.parseInt(end), quadCounter);
	}
}

void Arreglo() : {Token id; String op1, op2; int index = 0;}
{
	<TK_ARR> id = <TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET>

    	{
    		op1 = id.image;
	  		op2 = (String)pOperands.pop();

	  		Cuadruplo declaracion = new Cuadruplo("arr",op1,op2,"");
	  		quadCounter++;
	  		cuadruplos.addElement(declaracion); // Agregarlo a la queue de cuadruplos
	  		
    	}
	


	(
		<TK_EQ> <TK_LPAR> Exp() 

		{
			op1 = (String)pOperands.pop(); // Valor que se va a asignar
			op2 = "" + index; // Indice del arreglo

			Cuadruplo asignacion = new Cuadruplo("=", op1, op2, id.image);
			quadCounter++;

			index++;

			cuadruplos.addElement(asignacion); // Agregarlo a la queue de cuadruplos

		}

		(<TK_COMMA> Exp() 
		{

			op1 = (String)pOperands.pop(); // Valor que se va a asignar
			op2 = "" + index; // Indice del arreglo

			Cuadruplo asignacion2 = new Cuadruplo("=", op1, op2, id.image);
			quadCounter++;

			index++;

			cuadruplos.addElement(asignacion2); // Agregarlo a la queue de cuadruplos
		}

		)*

		<TK_RPAR>

	)?
}






// --------- FUNCIONES AUXILIARES -------------  //


// Punto 4 y 5, si hay operadores pendientes...
void PendingOperator(String operator1, String operator2) : {String o, op1,  op2, r;}
{
	{
		// Si la pila no esta vacía
		if( !pOperators.empty()) {

			// Y el tope de la pila es alguno de los operadores mandados. +- o */
			if ( pOperators.peek() == operator1 || pOperators.peek() == operator2  ) {

				// Sacar los operandos y el operador de las pilas
				op2 = (String)pOperands.pop();
				op1 = (String)pOperands.pop();
				o = (String)pOperators.pop();
				r = "t" + currentTemporal;

				currentTemporal++;

				// Crear un cuadruplo y meterlos
				Cuadruplo quad = new Cuadruplo(o, op1, op2, r);
				quadCounter++;

		    	pOperands.push(r);

		    	cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
		    }
		}
	}
}

// Punto 9, si hay operadores booleanos pendientes...
void PendingOperatorBool() : {String o, op1,  op2, r;}
{
	{
		// Si la pila no esta vacía
		if( !pOperators.empty()) {

			// Y el tope de la pila es > < <> o ==
			if ( pOperators.peek() == ">" || pOperators.peek() == "<" || 
				pOperators.peek() == "<>" || pOperators.peek() == "=="  ) {

				// Sacar los operandos y el operador de las pilas
				op2 = (String)pOperands.pop();
				op1 = (String)pOperands.pop();
				o = (String)pOperators.pop();
				r = "t" + currentTemporal;

				currentTemporal++;

				// Crear un cuadruplo y meterlos
				Cuadruplo quad = new Cuadruplo(o, op1, op2, r);
				quadCounter++;

		    	pOperands.push(r);

		    	cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
		    }
		}
	}
}

// Hace print a los cuadruplos
void MostrarCuadruplos() : {}
{
	{
		for (int i=0; i < cuadruplos.size(); i++) {
			System.out.print(i+1 + ": \t");
			Cuadruplo.displayCuadruplo((Cuadruplo)cuadruplos.get(i));
		}
		System.out.print("\n");
	}
}

// Llena el campo de jump de un cuadruplo
void Fill(int numQuad, int jump) : {}
{
	{
		Cuadruplo quad = (Cuadruplo)cuadruplos.get(numQuad);
		quad.resultado = "" + (jump+1);
	}
}