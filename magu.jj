/*
	MAGU Scanner/Parser
	Adrián Martínez A01280252
	Estefanía Guajardo A00813202
*/

// Para permitir el uso de variables y que los métodos no sean estáticos por default
options {
	STATIC = false ;
}

PARSER_BEGIN(magu)

import java.util.*;

class magu
{

	public static void main(String[] args) throws ParseException
	{
		try
		{
			magu scanner = new magu(System.in);
			scanner.Programa();
			System.out.println("OK!");
		}
		catch(ParseException e)
		{
			System.out.println(e.getMessage());
			System.out.println("Error!");
		}
	}

	Stack pOperators = new Stack();
	Stack pOperands = new Stack();
	Stack pJumps = new Stack();

	Vector cuadruplos = new Vector(1);

	int currentTemporal  = 1;
	int quadCounter = 0;
	
}
PARSER_END(magu)

TOKEN:
{
		< TK_FORWARD : "forward" | "f" >		{}
	|	< TK_LEFT : "left" | "l" >				{}
	|	< TK_RIGHT : "right" | "r">				{}
	|	< TK_PENCILUP : "pencilup" | "pu" >		{}
	|	< TK_PENCILDOWN : "pencildown" | "pd" >	{}
	|	< TK_MOVE : "move" | "m" >				{}
	|	< TK_HOME : "home" | "h" >				{}
	|	< TK_CURVE : "curve" | "cv" >			{}
	|	< TK_INPUT : "input" | "in" >			{}
	|	< TK_PRINT : "print" | "pr" >			{}
	|	< TK_WHILE : "while" >					{}
	|	< TK_IF : "if" >						{}
	|	< TK_ELSE : "else" >					{}
	|	< TK_ARR : "arr" >						{}
	|	< TK_FUNC : "func" >					{}
	|	< TK_MAIN : "main" >					{}
	|	< TK_EQ: "=" >							{}
	|	< TK_NE: "<>" >							{}
	|	< TK_EQT: "==" >						{}
	|	< TK_LT: "<" >							{}
	|	< TK_GT: ">" >							{}
	|	< TK_LPAR: "(" >						{}
	|	< TK_RPAR: ")" >						{}
	|	< TK_LBRACKET: "[" >					{}
	|	< TK_RBRACKET: "]" >					{}
	|	< TK_LBRACE: "{" >						{}
	|	< TK_RBRACE: "}" >						{}
	|	< TK_COMMA: "," >						{}
	|	< TK_PLUS: "+" >						{}
	|	< TK_MINUS: "-" >						{}
	|	< TK_MULT: "*" >						{}
	|	< TK_DIV: "/" >							{}	
	|	< TK_CTE_I : (["0"-"9"])+ >				{}
	|	< TK_ID :  ["a"-"z","A"-"Z"](["a"-"z","A"-"Z","0"-"9"])* > {}
	|   < TK_STRING : <QUOTE> (~["\r","\n","\"" ])+ <QUOTE> > {}
	|	< #QUOTE : "\"" >
}

SKIP:
{
	" "	| "\r\n" | "\t"
}

void Programa() : {String main;}
{   
    ( Asignacion() )*
    ( Arreglo() )*

    {
		Cuadruplo mainQuad = new Cuadruplo("Goto", "", "", "");
		quadCounter++;
		cuadruplos.addElement(mainQuad); // Agregarlo a la queue de cuadruplos

		pJumps.push("" + (quadCounter-1));
	}

    ( Definicion_Func() )*
    
    <TK_MAIN> <TK_LPAR> <TK_RPAR>

	{
		main = (String) pJumps.pop();
		System.out.println("Main starts at quadCounter: " + (quadCounter+1));
		Fill ( Integer.parseInt(main) , quadCounter);
	}

    Secuencia()  <EOF>

    {
    	Cuadruplo quad = new Cuadruplo("end","","",""); // Fin del codigo
    	cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
    	MostrarCuadruplos();
    }
}

void Func() : {String op1, op2; Token id;}
{
    ( 
    	  <TK_FORWARD> <TK_LPAR> Exp() <TK_RPAR> 
    	  	{
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("f",op1,"","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_LEFT> <TK_LPAR> Exp() <TK_RPAR>
    	  	{
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("l",op1,"","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_RIGHT> <TK_LPAR>  Exp() <TK_RPAR>
    	  	{
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("r",op1,"","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_INPUT> <TK_LPAR> id = <TK_ID> <TK_RPAR>
    	  	{
    	  		op1 = id.image;

    	  		Cuadruplo quad = new Cuadruplo("in",op1,"","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
	    	
    	| <TK_PRINT> <TK_LPAR> 
    			( 
    				LOOKAHEAD(2)
    				<TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET>
    			|
    				<TK_ID> 					
    			|
    				<TK_STRING>
    			)

    			( <TK_PLUS> 
    				( 
    					LOOKAHEAD(2)
    			  		<TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET> 	
    			  	|
    					<TK_ID>
    				|
    					<TK_STRING>
    				) 
    			)*
    			<TK_RPAR>

    	| <TK_PENCILUP> <TK_LPAR> <TK_RPAR>
    	  	{
    	  		Cuadruplo quad = new Cuadruplo("pu","","","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_PENCILDOWN> <TK_LPAR> <TK_RPAR>
    	  	{
    	  		Cuadruplo quad = new Cuadruplo("pd","","","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_MOVE> <TK_LPAR> Exp() <TK_COMMA> Exp() <TK_RPAR>
    	  	{
    	  		op2 = (String)pOperands.pop();
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("m",op1,op2,"");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_CURVE> <TK_LPAR> Exp() <TK_COMMA> Exp() <TK_RPAR>
    	  	{
    	  		op2 = (String)pOperands.pop();
    	  		op1 = (String)pOperands.pop();

    	  		Cuadruplo quad = new Cuadruplo("cv",op1,op2,"");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    	| <TK_HOME> <TK_LPAR> <TK_RPAR>
    	  	{
    	  		Cuadruplo quad = new Cuadruplo("h","","","");
    	  		quadCounter++;
    	  		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	    	}
    )   
}  





void Funcion() : {Token id; int paramCounter = 0, start; String argument;} 
{	
    id = <TK_ID>

    {
    	// Verify that a function with this id exists
    } 


    <TK_LPAR> 
    {
    	// Generate function call quad. ERA size equivalent, not necessary
    	
    	Cuadruplo func = new Cuadruplo("func",id.image,"","");
    	quadCounter++;
    	cuadruplos.addElement(func); // Agregarlo a la queue de cuadruplos
    }

    // ARGUMENTS
    ( 
    	Exp()
    	{
    		paramCounter++;
    		argument = (String)pOperands.pop();

    		Cuadruplo quad = new Cuadruplo("param",argument,"","param" + paramCounter);
    		quadCounter++;
    		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
    	}

    	( <TK_COMMA> {paramCounter++;}
    		Exp() 
    		{
	    		argument = (String)pOperands.pop();

	    		Cuadruplo quad2 = new Cuadruplo("param",argument,"","param" + paramCounter);
	    		quadCounter++;
	    		cuadruplos.addElement(quad2); // Agregarlo a la queue de cuadruplos
    		}
    	)* 
    )? 

    <TK_RPAR>
    {
    	// Verify that paramCounter is the same as the number of params in the func definition


    	// Generate gosub, procname, address where it starts
    	start = TablaFunciones.getFuncStart(id.image);

    	Cuadruplo gosub = new Cuadruplo("gosub",id.image,"", "" + start );
	    quadCounter++;
	    cuadruplos.addElement(gosub); // Agregarlo a la queue de cuadruplos
    }
}






void Exp() : {}
{
	Termino()
	PendingOperator("+","-")

	( 
		<TK_PLUS> 
		{
			pOperators.push("+"); // meter el + a la stack de operadores
		}

		Termino()
		PendingOperator("+","-")

		| 

		<TK_MINUS> 
		{
			pOperators.push("-"); // meter el - a la stack de operadores
		}

		Termino()
		PendingOperator("+","-")

	)*
		
}

void Termino() : {}
{
	Factor()
	
	( 
		<TK_MULT> 
		{
			pOperators.push("*"); // meter el * a la stack de operadores
		}

		Factor()
		PendingOperator("*","/")

		| 

		<TK_DIV>
		{
			pOperators.push("/"); // meter el / a la stack de operadores
		}

		Factor()
		PendingOperator("*","/")

	)*

}

void Factor() : {Token o; String index;}
{
	LOOKAHEAD(2)

	//indice de arreglo
	o = <TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET>
	{
		index = (String)pOperands.pop();
		pOperands.push(o.image + "-" + index );
	}

	|

	// variable
	o = <TK_ID>
	{
		pOperands.push(o.image); // meter el id a la stack de operandos
	}

	|

	//constante
	o = <TK_CTE_I>
	{
		pOperands.push(o.image); // meter la constante a la stack de operandos
	}
	
}


void Asignacion() : {String op1, op2; Token r;}
{
	LOOKAHEAD(2) 
	// variable
	r = <TK_ID> <TK_EQ> Exp()
	{
		op1 = (String)pOperands.pop();

		Cuadruplo quad = new Cuadruplo("=", op1, "", r.image);
		quadCounter++;

		//System.out.println("quadCounter: " + quadCounter);
		//Cuadruplo.displayCuadruplo(quad); 

		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos

	}
	|
	// indice de arreglo
	r = <TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET> <TK_EQ> Exp()
	{
		op1 = (String)pOperands.pop(); // Valor que se va a asignar
		op2 = (String)pOperands.pop(); // Indice del arreglo

		Cuadruplo quad = new Cuadruplo("=", op1, op2, r.image);
		quadCounter++;

		//System.out.println("quadCounter: " + quadCounter);
		//Cuadruplo.displayCuadruplo(quad); 

		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos

	}
}


void Bool() : {Token o;}
{
	Exp()
	( 
		o = <TK_LT> Exp()
		{
			pOperators.push(o.image); // meter el < a la stack de operandos
		}
		PendingOperatorBool()

		|
		
		o = <TK_GT>	Exp()
		{
			pOperators.push(o.image); // meter el > a la stack de operandos
		}
		PendingOperatorBool()


		|
		
		o = <TK_NE> Exp()
		{
			pOperators.push(o.image); // meter el <> a la stack de operandos
		}
		PendingOperatorBool()


		|
		
		o = <TK_EQT> Exp()
		{
			pOperators.push(o.image); // meter el == a la stack de operandos
		}
		PendingOperatorBool()


	)?
}

void Secuencia() : {}
{
	<TK_LBRACE> ( LOOKAHEAD(2) Funcion() | LOOKAHEAD(2) Func() | While() | Asignacion() | Condicion() )
	( <TK_COMMA> ( LOOKAHEAD(2) Funcion() | LOOKAHEAD(2) Func() | While() | Asignacion() | Condicion() ) )*
	<TK_RBRACE>
}

void While() : {String op1, r;}
{
	<TK_WHILE>
	{
		pJumps.push("" + (quadCounter+1));
	}


	<TK_LPAR> Bool() <TK_RPAR>
	{
		op1 = (String)pOperands.pop();

		Cuadruplo quadTrue = new Cuadruplo("GotoF", op1, "", "");
		quadCounter++;
		cuadruplos.addElement(quadTrue); // Agregarlo a la queue de cuadruplos
		pJumps.push("" + (quadCounter-1));
	}

	Secuencia()
	{

		String end = (String)pJumps.pop();
		String retorno = (String)pJumps.pop();

		Cuadruplo quadFalse = new Cuadruplo("Goto", "", "", retorno);
		quadCounter++;
		cuadruplos.addElement(quadFalse); // Agregarlo a la queue de cuadruplos

		Fill ( Integer.parseInt(end), quadCounter);
	}
}







void Definicion_Func() : {Token id, var; int numParam = 0;}
{
	<TK_FUNC> id = <TK_ID>
	{
		// 1.- Insert func name into DirFunc table
		TablaFunciones.addFunc(id.image);

	}

	
	// Insert into dirFunc, num of parameters defined.

	// Insert into dirFunc, num of local variables defined (?)

	
	<TK_LPAR>
	( 
		// 2- Insert every parameter into function's VarTable
		var = <TK_ID>
		{
			numParam++;
		}

		( 
			<TK_COMMA> 
			var = <TK_ID>
			{
				numParam++;
			}			
		)* 

	)?
	<TK_RPAR>

	// Insert into dirFunc current quadCounter. This is where functions starts. 
	{
		System.out.println( id.image + " starts at quadCounter: " + (quadCounter+1));

		TablaFunciones.setFuncStart(id.image, quadCounter+1 );
	}

	Secuencia()
	{
		Cuadruplo quad = new Cuadruplo("endproc", "", "", "");
		quadCounter++;
		cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
	}

	
}










void Condicion() : {String op1, r;}
{
	<TK_IF> <TK_LPAR> Bool() <TK_RPAR>

	{
		op1 = (String)pOperands.pop();

		Cuadruplo quadTrue = new Cuadruplo("GotoF", op1, "", "");
		quadCounter++;

		//System.out.println("quadCounter: " + quadCounter);

		//Cuadruplo.displayCuadruplo(quadTrue);

		cuadruplos.addElement(quadTrue); // Agregarlo a la queue de cuadruplos

		pJumps.push("" + (quadCounter-1));
	}
	Secuencia()

	(<TK_ELSE> 
	{
		Cuadruplo quadFalse = new Cuadruplo("Goto", "", "", "");
		quadCounter++;

		//System.out.println("quadCounter: " + quadCounter);


		//Cuadruplo.displayCuadruplo(quadFalse);

		cuadruplos.addElement(quadFalse); // Agregarlo a la queue de cuadruplos

		String falso = (String)pJumps.pop();
		pJumps.push("" + (quadCounter-1));

		//System.out.println("falso: " + falso);


		Fill ( Integer.parseInt(falso), quadCounter);

	}

		Secuencia() )?

	// 2, aquí termina el if. A dónde brinca?
	{
		String end = (String)pJumps.pop();

		//System.out.println("end: " + end);

		Fill ( Integer.parseInt(end), quadCounter);
	}
}

void Arreglo() : {Token id; String op1, op2; int index = 0;}
{
	<TK_ARR> id = <TK_ID> <TK_LBRACKET> Exp() <TK_RBRACKET>

    	{
    		op1 = id.image;
	  		op2 = (String)pOperands.pop();

	  		//System.out.println("El op2 es: " + op2);

	  		//size = (int)pOperands.pop();
	  		//TablaVariables.declararArreglo(var, new int[size]);

	  		Cuadruplo declaracion = new Cuadruplo("arr",op1,op2,"");
	  		quadCounter++;
	  		cuadruplos.addElement(declaracion); // Agregarlo a la queue de cuadruplos
	  		
    	}
	


	(
		<TK_EQ> <TK_LPAR> Exp() 

		{
			op1 = (String)pOperands.pop(); // Valor que se va a asignar
			op2 = "" + index; // Indice del arreglo

			Cuadruplo asignacion = new Cuadruplo("=", op1, op2, id.image);
			quadCounter++;

			index++;

			cuadruplos.addElement(asignacion); // Agregarlo a la queue de cuadruplos

		}

		(<TK_COMMA> Exp() 
		{

			op1 = (String)pOperands.pop(); // Valor que se va a asignar
			op2 = "" + index; // Indice del arreglo

			Cuadruplo asignacion2 = new Cuadruplo("=", op1, op2, id.image);
			quadCounter++;

			index++;

			cuadruplos.addElement(asignacion2); // Agregarlo a la queue de cuadruplos
		}

		)*

		<TK_RPAR>

	)?
}






// --------- FUNCIONES AUXILIARES -------------  //


// Punto 4 y 5, si hay operadores pendientes...
void PendingOperator(String operator1, String operator2) : {String o, op1,  op2, r;}
{
	{
		// Si la pila no esta vacía
		if( !pOperators.empty()) {

			// Y el tope de la pila es alguno de los operadores mandados. +- o */
			if ( pOperators.peek() == operator1 || pOperators.peek() == operator2  ) {

				// Sacar los operandos y el operador de las pilas
				op2 = (String)pOperands.pop();
				op1 = (String)pOperands.pop();
				o = (String)pOperators.pop();
				r = "t" + currentTemporal;

				currentTemporal++;

				//System.out.println( r );

				// Crear un cuadruplo y meterlos
				Cuadruplo quad = new Cuadruplo(o, op1, op2, r);
				quadCounter++;

		    	pOperands.push(r);

		    	//Cuadruplo.displayCuadruplo(quad);

		    	cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
		    }
		}
	}
}

// Punto 9, si hay operadores booleanos pendientes...
void PendingOperatorBool() : {String o, op1,  op2, r;}
{
	{
		// Si la pila no esta vacía
		if( !pOperators.empty()) {

			// Y el tope de la pila es > < <> o ==
			if ( pOperators.peek() == ">" || pOperators.peek() == "<" || 
				pOperators.peek() == "<>" || pOperators.peek() == "=="  ) {

				// Sacar los operandos y el operador de las pilas
				op2 = (String)pOperands.pop();
				op1 = (String)pOperands.pop();
				o = (String)pOperators.pop();
				r = "t" + currentTemporal;

				currentTemporal++;

				//System.out.println( r );

				// Crear un cuadruplo y meterlos
				Cuadruplo quad = new Cuadruplo(o, op1, op2, r);
				quadCounter++;

		    	pOperands.push(r);

		    	//Cuadruplo.displayCuadruplo(quad);

		    	cuadruplos.addElement(quad); // Agregarlo a la queue de cuadruplos
		    }
		}
	}
}



void MostrarCuadruplos() : {}
{
	{
		for (int i=0; i < cuadruplos.size(); i++) {
			System.out.print(i+1 + ": \t");
			Cuadruplo.displayCuadruplo((Cuadruplo)cuadruplos.get(i));
			
		}
	}
}

void Fill(int numQuad, int jump) : {}
{
	{
		Cuadruplo quad = (Cuadruplo)cuadruplos.get(numQuad);

		quad.resultado = "" + (jump+1);

		//System.out.print("Se acaba de hacer un fill: ");
		//Cuadruplo.displayCuadruplo(quad);

	}
}